/*
 * Copyright 2019 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
	ext {
		kotlin_version = '1.3.72'
	}

	repositories {
		mavenLocal()
		google()
		mavenCentral()
		jcenter()

		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	ext {
		minSdk = 23
		maxSdk = 30

		// Us
		pydroid = "21.0.6-SNAPSHOT"

		// Room
		room = "2.2.5"

		// Leak Canary
		leakCanary = "2.4"

		// Dagger
		dagger = "2.28"

		moshi = "1.9.3"

		// Fast Adapter
		fastAdapter = "5.1.0"

		workmanager = "2.3.4"

		osmdroid = "6.1.6"

		// FusedLocationProvider
		gmsLocation = "17.0.0"

		balloon = "1.1.7"
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:4.0.0'

		// Gradle version checker
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.28.0'

		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

		// Spotless
		classpath "com.diffplug.spotless:spotless-plugin-gradle:4.3.0"

		//        classpath 'com.google.gms:google-services:4.3.2'
	}
}

allprojects {
	apply plugin: "com.github.ben-manes.versions"
	apply plugin: "com.diffplug.gradle.spotless"

	spotless {
		java {
			target "**/*.java"
		}
		kotlin {
			target "**/*.kt"
			ktlint()
		}
		groovyGradle {
			target "*.gradle", "**/*.gradle"
			greclipse()
		}
	}

	// Ignore non-release libraries
	dependencyUpdates.resolutionStrategy {
		componentSelection { rules ->
			rules.all { selection ->
				boolean rejected = [
					'alpha',
					'beta',
					'rc',
					'eap',
					'cr',
					'm',
					'preview'
				].any { qualifier ->
					selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
				}
				if (rejected) {
					selection.reject('Release candidate')
				}
			}
		}
	}

	repositories {
		mavenLocal()
		google()
		mavenCentral()
		jcenter()

		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
		options.deprecation = true
	}
}
